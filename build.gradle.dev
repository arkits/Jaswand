apply plugin: 'java-library'
apply plugin: 'maven'
apply plugin: 'signing'

sourceCompatibility = 1.8
targetCompatibility = 1.8

group = 'io.github.arkits'
version = '0.0.4'

repositories {
    jcenter()
    mavenCentral()
    mavenLocal()
}

dependencies {
    // This dependency is exported to consumers, that is to say found on their compile classpath.
    api 'org.apache.commons:commons-math3:3.6.1'

    // This dependency is used internally, and not exposed to consumers on their own compile classpath.
    implementation 'com.google.guava:guava:28.0-jre'

    // Use JUnit test framework
    testImplementation 'junit:junit:4.12'

    // For rendering HTML files
    compile group: 'com.j2html', name: 'j2html', version: '1.3.0'

    // For JSON stuff
    implementation 'com.google.code.gson:gson:2.8.6'
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from 'build/docs/javadoc'
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allSource
    classifier = 'sources'
}

artifacts {
    archives jar
    archives javadocJar
    archives sourcesJar
}

signing {
    sign configurations.archives
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
              authentication(userName: sonatypeUsername, password: sonatypePassword)
            }

            pom.project {
               name 'Jaswand'
               packaging 'jar'
               description 'Create beautiful reports, right from Java!'
               url 'https://github.com/arkits/jaswand/'

               scm {
                   url 'scm:git@github.com:arkits/jaswand.git'
                   connection 'scm:git@github.com:arkits/jaswand.git'
                   developerConnection 'scm:git@github.com:arkits/jaswand.git'
               }

               licenses {
                   license {
                       name 'MIT License'
                       url 'https://github.com/arkits/jaswand/blob/master/LICENSE'
                       distribution 'repo'
                   }
               }

               developers {
                   developer {
                       id 'arkits'
                       name 'Archit Khode'
                   }
               }
           }
        }
    }
}